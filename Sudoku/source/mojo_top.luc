module mojo_top (
    input clk,              // 50MHz clock
    input rst_n,            // reset button (active low)
    output led [8],         // 8 user controllable LEDs
    //input cclk,             // configuration clock, AVR ready when high
    output spi_miso,        // AVR SPI MISO
    //input spi_ss,           // AVR SPI Slave Select
    //input spi_mosi,         // AVR SPI MOSI
    //input spi_sck,          // AVR SPI Clock
    output spi_channel [4], // AVR general purpose pins (used by default to select ADC channel)
    //input avr_tx,           // AVR TX (FPGA RX)
    output avr_rx,          // AVR RX (FPGA TX)
    //input avr_rx_busy,      // AVR RX buffer full
    input io_dip [3][8],    // DIP switches on IO Shield
    input io_button[5],
    output led_strip[8],
    output io_led[3][8]
  ) {
  
  sig rst;                  // reset signal
  fsm state(.clk(clk), .rst(rst)) = {GET_PRNG, INIT_BOARD, WAIT, CHECK, WRONG, DONE};
  dff wrong_counter[25](.clk(clk), .rst(rst)); // counter for flashing display when wrong answer is given
  board_generator gen;
  
  .clk(clk) {
    // The reset conditioner is used to synchronize the reset signal to the FPGA
    // clock. This ensures the entire FPGA comes out of reset at the same time.
    reset_conditioner reset_cond;
    button_conditioner button_cond;
    edge_detector edge_detector(#RISE(1), #FALL(0));
    
    .rst(rst) {
      prng prng;
      board_manager board_mgr;
      mask_manager mask_mgr;
      board_checker chkr;
      display display;
    }
  }
  
  always {
    reset_cond.in = ~rst_n;   // input raw inverted reset signal
    rst = reset_cond.out;     // conditioned reset
    io_led = {8h00, 8h00, 8h00};
    io_led[0][1:0] = prng.value[1:0];
    // components for button pressing
    button_cond.in = io_button[1];
    edge_detector.in = button_cond.out;
    // seeding prng
    prng.seed = io_dip[2];
    // permanent value for board_generator
    gen.n = prng.value[1:0];
    // default variables for board_manager
    board_mgr.write = 0;
    board_mgr.toggle = 0;
    board_mgr.row = 3b0;
    board_mgr.col = 3b0;
    board_mgr.data = board_mgr.out;
    // default variables for mask_manager
    mask_mgr.write_state = 2b0;
    mask_mgr.row = 3b0;
    mask_mgr.col = 3b0;
    // const variables for board_checker
    chkr.en = 0;
    chkr.board = board_mgr.out;
    chkr.ans = gen.ans;
    // const variables for display
    display.data = board_mgr.out;
    display.mask = mask_mgr.out;
    led_strip = display.out;
    
    case (state.q) {
      state.GET_PRNG:
        // wait for prng to complete its calculations
        if (prng.read_enable) state.d = state.INIT_BOARD;
      state.INIT_BOARD:
        // writes initial board to the manager
        board_mgr.write = 1;
        board_mgr.data = gen.init;
        state.d = state.WAIT;
      state.WAIT:
        if (edge_detector.out == 1) state.d = state.CHECK;
      state.CHECK:
        chkr.en = 1;
        if (chkr.read_enable) state.d = chkr.out ? state.DONE : state.WRONG;
      state.WRONG:
        wrong_counter.d = wrong_counter.q + 1;
        if (wrong_counter.q == 25h1ffffff) state.d = state.WAIT;
        display.data = wrong_counter.q[23] ? gen.empty : gen.wrong;
      state.DONE:
        display.data = gen.done;
    }
    
    led = 8h00;               // turn LEDs off
    spi_miso = bz;            // not using SPI
    spi_channel = bzzzz;      // not using flags
    avr_rx = bz;              // not using serial port
  }    
}